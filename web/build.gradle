plugins {
    id "org.asciidoctor.convert" version "1.5.2"
}
apply plugin: 'war'
dependencies {
    compile project(':service')
    compile spring_mvc
    compile jackson
    compile apache_commons_lang
    compile apache_commons_collections4
    compile apache_commons_commons_beanutils
    compile servlet_api
    compile hibernate_validator
    compile spring_security_config
    compile spring_security_web
    compile jjwt
    compile ('org.springframework.boot:spring-boot-starter-web:1.5.1.RELEASE') {
        exclude group: 'ch.qos.logback'
    }

    testCompile spring_rest_docs
    testCompile hsqldb
    testCompile javax_el
}

war {
    dependsOn asciidoctor
    from("${asciidoctor.outputDir}/html5") {
        into 'docs'
    }
}

ext {
    snippetsDir = file('build/generated-snippets')
}


test {
    outputs.dir snippetsDir
    exclude 'edu/softserve/zoo/web/test/controller/docs/**'
}
task docsTest(type: Test) {
    include 'edu/softserve/zoo/web/test/controller/docs/**'
}

asciidoctor {
    attributes 'snippets': snippetsDir
    inputs.dir snippetsDir
    outputDir "build/asciidoc"
    dependsOn docsTest
    sourceDir 'src/test/resources/asciidoc'
}
